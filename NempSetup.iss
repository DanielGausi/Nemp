; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Nemp - Noch ein MP3-Player"
#define MyAppVersion "5.0.0"
#define MyAppPublisher "Daniel Gauﬂmann"
#define MyAppURL "https://www.gausi.de"
#define MyAppExeName "nemp.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{D4FC64F7-CC71-4917-A1D2-254B8F55733E}-Nemp
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=licence.txt
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputDir=_Setup\
OutputBaseFilename=NempSetup
SetupIconFile=src\New Icons\NempMainIcon.ico
UninstallDisplayIcon={app}\nemp.exe,0
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[InstallDelete]
Type: files; Name: "{app}\UseLocalData.cfg"
Type: files; Name: "{app}\DONT_UseLocalData.cfg"


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl,nemp_en.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl,nemp_ger.isl"; LicenseFile: "licence_DE.txt"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "bin\nemp.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "bin\UseLocalData.cfg"; DestDir: "{app}"; DestName:"UseLocalData.cfg"; Check: IsModeSelected(1)
Source: "bin\UseLocalData.cfg"; DestDir: "{app}"; DestName:"DONT_UseLocalData.cfg"; Check: IsModeSelected(0)
Source: "bin\*"; Excludes: "UseLocalData.cfg"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
var
  UsagePage: TInputOptionWizardPage;
  UsagePageID: Integer; 

function IsModeSelected(Mode: Integer): Boolean;
begin
  Result := (UsagePage.SelectedValueIndex = Mode);
end;

function NextButtonClick(apage: Integer): boolean;
begin
  if  aPage=wpSelectDir then 
  begin     
    if (pos(ExpandConstant('{commonpf}'), WizardDirValue) = 1) then  
      UsagePage.SelectedValueIndex := 0       
    else
      UsagePage.SelectedValueIndex := 1; 
  end;    
  result := True;
end;

procedure InitializeWizard();
begin 
  UsagePage :=
    CreateInputOptionPage(
      wpSelectDir, CustomMessage('InstallMode'), CustomMessage('InstallModeHint'), CustomMessage('InstallModeSubCaption'), True, False);
  UsagePage.Add(CustomMessage('DontUseLocalData'));
  UsagePage.Add(CustomMessage('UseLocalData'));
  UsagePageID := UsagePage.ID;
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
    result := False;
    if PageID = UsagePageID then
    begin
        if (pos(ExpandConstant('{commonpf}'), WizardDirValue) = 1) then
        begin
            UsagePage.SelectedValueIndex := 0
            result := true;
        end;
    end;
end;	        

[UninstallDelete]
Type: files; Name: "{userappdata}\Gausi\Nemp\Nemp.ini"
Type: files; Name: "{userappdata}\Gausi\Nemp\CoverCache"
Type: files; Name: "{userappdata}\Gausi\Nemp\Hotkeys.ini"
Type: files; Name: "{userappdata}\Gausi\Nemp\Nemp.npl"
Type: files; Name: "{userappdata}\Gausi\Nemp\Nemp.gmp"
Type: files; Name: "{userappdata}\Gausi\Nemp\NempWebServer.ini"
Type: files; Name: "{userappdata}\Gausi\Nemp\tag_ignore"
Type: files; Name: "{userappdata}\Gausi\Nemp\tag_merge"
Type: files; Name: "{userappdata}\Gausi\Nemp\temp.npl"
Type: files; Name: "{userappdata}\Gausi\Nemp\temp.old.npl"
Type: files; Name: "{userappdata}\Gausi\Nemp\Nemp_EQ.ini"
Type: files; Name: "{userappdata}\Gausi\Nemp\RandomPlaylist.ini"
Type: files; Name: "{userappdata}\Gausi\Nemp\Nemp-PlayerLog.log"
Type: filesandordirs; Name: "{userappdata}\Gausi\Nemp\Cover"
Type: dirifempty; Name: "{userappdata}\Gausi\Nemp\Webradio"
Type: dirifempty; Name: "{userappdata}\Gausi\Nemp"
Type: dirifempty; Name: "{userappdata}\Gausi"
